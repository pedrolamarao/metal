plugins
{
    id 'cpp'
}

subprojects 
{
    // settings

    apply plugin: AsmLanguage
    apply plugin: 'aasgard.gdb'
    apply plugin: 'cpp-application'
    apply plugin: 'pl.gradle-qemu'
    
    // project
    
    application
    {
        // #XXX: Gradle can't cross compile to system "none"
        targetMachines = [ machines.linux.x86, machines.windows.x86 ]
        
        dependencies
        {
            implementation project(':multiboot2')
        }
    
        binaries.whenElementFinalized { binary ->
        
            final compiler = compileTask.get()
            compiler.compilerArgs.addAll '-std=c++20', '-Werror', '-Wall'
            
            final linker = linkTask.get()
            linker.linkerArgs.addAll '-std=c++20', '-Wl,-Ttext=0x1000'
    
            final suffix = binary.name.capitalize()
    
            final grubRescue = project.tasks.register("grubRescue${suffix}", CreateMultibootRescue) {
                inputFile = binary.executableFile
            }
            
            def qemuRescue = qemu.registerSystem("qemuRescue${suffix}") {
                architecture = 'i386'
                cdrom = grubRescue.get().outputFile
                gdb = 'tcp::12345'
                start = false
                tool = "${toolsDir}/usr/bin/qemu-system-i386"
            }
            
            def gdbTest = gdb.testListener('_test_result')
            
            def gdbRescue = gdb.register("gdbRescue${suffix}") {
                dependsOn grubRescue
                tool = "${toolsDir}/usr/bin/i686-pc-elf-gdb"
                listeners.add gdbTest
                listeners.add gdb.logListener(file("build/tmp/gdbRescue${suffix}/mi.txt"))
                target = binary.executableFile
                script {
                    def qp = qemu.runSystem {
                        architecture = 'i386'
                        cdrom = grubRescue.get().outputFile
                        gdb = 'tcp:localhost:12345'
                        display = 'none'
                        start = false
                        tool = "${toolsDir}/usr/bin/qemu-system-i386"
                    }
                    push 'set output-radix 16'
                    push 'target remote localhost:12345'
                    push 'thbreak _start'
                    push 'continue'
                    push 'watch _test_result'
                    push 'continue'
                    synchronized (gdbTest) { gdbTest.wait(5000); }
                    logger.lifecycle '_test_result = ' + gdbTest.value
                    qp.destroy()
                    push 'quit'
                    waitFor 5000
                }
            }
            
            // #XXX: add "debug" variant to qemuSystemTest
            if (! binary.optimized) { qemuSystemTest.dependsOn gdbRescue }
        }
    }
}

plugins
{
    id 'base'
}

subprojects 
{
    // settings

    apply plugin: 'br.dev.pedrolamarao.gdb'
    apply plugin: 'dev.nokee.cpp-application'

    // project
    
    application
    {
        // #XXX: Nokee can't cross compile to system "none"
        final none = org.gradle.internal.os.OperatingSystem.current().getName()
        targetMachines = [ machines.os(none).architecture('-multiboot-x86_32') ]
        
        dependencies
        {
            implementation project(':multiboot2')
        }

        final baseArgs = [ '-std=c++20', '-flto', '-fasm-blocks' ]

        binaries.configureEach {
            compileTasks.configureEach {
                compilerArgs.addAll baseArgs
            }
            linkTask.get().with {
                linkerArgs.addAll baseArgs
                linkerArgs.addAll '-Wl,-Ttext=0x1000'
            }
        }
    }

    final createImage = tasks.register('createImage', CreateMultibootImage) {
        group = 'psys'
        dependsOn assemble
        inputFile = providers.provider {
            application.binaries
                .filter { it.isBuildable() }
                .map { it.get(0).linkTask.get().linkedFile.get() }
                .get()
        }
    }

    tasks.register('runImage', RunMultibootImage) {
        group = 'psys'
        imageFile = createImage.get().outputFile
    }

    def testImage = tasks.register('testImage', TestMultibootImage) {
        group = 'psys'
        executableFile = providers.provider {
            application.binaries
                .filter { it.isBuildable() }
                .map { it.get(0).linkTask.get().linkedFile.get() }
                .get()
        }
        imageFile = createImage.get().outputFile
    }

    check.dependsOn testImage

    parent.tasks.named('check') { dependsOn check }
}

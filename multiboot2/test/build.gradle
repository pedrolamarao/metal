plugins
{
    id 'cpp-application'
}

subprojects 
{
    // settings

    apply plugin: 'aasgard.asm'
    apply plugin: 'aasgard.gdb'
    apply plugin: 'aasgard.grub'
    apply plugin: 'aasgard.qemu'
    apply plugin: 'cpp-application'

    // project
    
    group 'aasgard'
    version '0.1.0-SNAPSHOT'
    
    application
    {
        // XXX: Gradle 6.1 refuses to cross compile
        // XXX: convince Gradle 6.1 to configure compiler to 32 bits for now
        targetMachines = [ machines.linux.x86 ]
    
        binaries.whenElementFinalized { binary ->
        
            def compiler = compileTask.get()
            compiler.compilerArgs.addAll '-std=c++17', '-ffreestanding', '-Wall', '-Werror'
            
            def linker = linkTask.get()
            linker.linkerArgs.addAll '-nostdlib', '-Ttext', '0x1000'
    
            def suffix = binary.name.capitalize()
    
            def grubRescue = grub.registerRescue("grubRescue${suffix}") {
                executable binary
            }
            
            def qemuRescue = qemu.registerSystem("qemuRescue${suffix}") {
                architecture = 'i386'
                cdrom = grubRescue.get().target
                gdb = 'tcp::1234'
                start = false
            }
            
            def gdbTest = gdb.testListener('_test_result')
            
            def gdbRescue = gdb.register("gdbRescue${suffix}") {
                dependsOn grubRescue
                executable = 'i686-pc-elf-gdb'
                listeners.add gdbTest
                listeners.add gdb.logListener(file("build/tmp/gdbRescue${suffix}/mi.txt"))
                target = binary.executableFile
                script {
                    def qp = qemu.system {
                        architecture = 'i386'
                        cdrom = grubRescue.get().target
                        gdb = 'tcp::1234'
                        display = 'none'
                        start = false
                    }
                    push 'set output-radix 16'
                    push 'target remote localhost:1234'
                    push 'thbreak _start'
                    push 'continue'
                    push 'watch _test_result'
                    push 'continue'
                    synchronized (gdbTest) { gdbTest.wait(5000); }
                    logger.lifecycle '_test_result = ' + gdbTest.value
                    qp.destroy()
                    push 'quit'
                    waitFor 5000
                }
            }
            
            if (! binary.optimized) { check.dependsOn gdbRescue }
        }
    }
    
    dependencies
    {
        implementation project(':multiboot2')
    }
}

check.configure {
    subprojects.each { dependsOn("${it.name}:check") }
}

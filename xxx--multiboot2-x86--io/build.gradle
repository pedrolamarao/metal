// settings

plugins
{
    id 'aasgard.asm' version '0.1.0-SNAPSHOT'
    id 'aasgard.grub' version '0.1.0-SNAPSHOT'
    id 'cpp-application'
}

// project

group 'aasgard'
version '0.1.0-SNAPSHOT'

tasks.register('grub') {
    group 'build'
    description 'Generates GRUB images'
}

application
{
    // XXX: Gradle 6.1 refuses to cross compile
    // XXX: convince Gradle 6.1 to configure compiler to 32 bits for now
    targetMachines = [ machines.linux.x86 ]

    binaries.whenElementFinalized { binary ->
    
        def compiler = compileTask.get()
        compiler.compilerArgs.addAll '-std=c++17', '-ffreestanding'
        
        def linker = linkTask.get()
        linker.linkerArgs.addAll '-nostdlib', '-Ttext', '0x1000'
        
        def grubPc = tasks.register("grubPc_${binary.name}", aasgard.gradle.grub.GrubStandaloneCompile) {
            binaryName binary.name
            format 'i386-pc'
            install 'normal', 'at_keyboard'
            sources 'src/main/grub', linker
        }

        def grubQemu = tasks.register("grubQemu_${binary.name}", aasgard.gradle.grub.GrubStandaloneCompile) {
            binaryName binary.name
            format 'i386-qemu'
            sources 'src/main/grub', linker
        }

        def grubRescue = tasks.register("grubRescue_${binary.name}", aasgard.gradle.grub.GrubRescueCompile) {
            binaryName binary.name
            sources 'src/main/grub', linker
        }
        
        grub.dependsOn grubPc, grubQemu, grubRescue
    }
}

dependencies
{
	implementation project(':multiboot2')
	implementation project(':pc')
	implementation project(':x86')
}

// tasks

assemble.dependsOn grub


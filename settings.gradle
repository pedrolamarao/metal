pluginManagement
{
    plugins
    {
        id 'br.dev.pedrolamarao.gdb' version '1.0.0-SNAPSHOT'
    }

    repositories
    {
        maven {
            name = "Nokee Release Repository"
            url = uri("https://repo.nokee.dev/release")
            mavenContent {
                includeGroupByRegex("dev\\.nokee.*")
                includeGroupByRegex("dev\\.gradleplugins.*")
            }
        }
        maven {
            name = "Nokee Snapshot Repository"
            url = uri("https://repo.nokee.dev/snapshot")
            mavenContent {
                includeGroupByRegex("dev\\.nokee.*")
                includeGroupByRegex("dev\\.gradleplugins.*")
            }
        }
        maven {
            name = 'Sonatype Open Source Snapshot Repository'
            url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
        gradlePluginPortal()
    }

    resolutionStrategy {
        eachPlugin {
            if (requested.id.id.startsWith("dev.nokee.")) {
                useModule("${requested.id.id}:${requested.id.id}.gradle.plugin:0.4.0-202112012027.ff552c41")
            }
        }
    }
}

rootProject.name = 'psys'

// components

include 'acpi'

include 'googletest'

include 'multiboot2'
new File(rootDir, 'multiboot2/test/x86_32-elf').eachDir {
    include "multiboot2:test:x86_32-elf:${it.name}"
}

include 'pc'
new File(rootDir, 'pc/test/x86_32-elf-multiboot2').eachDir {
    include "pc:test:x86_32-elf-multiboot2:${it.name}"
}

include 'psys'

include 'x86'
new File(rootDir, 'x86/test/x86_32-elf-multiboot2').eachDir {
    include "x86:test:x86_32-elf-multiboot2:${it.name}"
}

// applications

include 'app:sandbox'
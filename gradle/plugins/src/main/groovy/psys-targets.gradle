final llvmPath = providers.provider { rootProject.ext.tools['br.dev.pedrolamarao.psys.llvm.path'] }

final x86_32_elf_multiboot2_ld = rootProject.file('multiboot2/x86_32-elf.ld')
final x86_64_elf_multiboot2_ld = rootProject.file('multiboot2/x86_64-elf.ld')

final host = {
    assembler.executable = 'clang'
    cCompiler.executable = 'clang'
    cppCompiler.executable = 'clang++'
    linker.executable = 'clang'
    linker.withArguments { '-fuse-ld=lld' }
    staticLibArchiver.executable = 'llvm-ar'
}

final multiboot_x86_32 = {
    assembler.executable = 'clang'
    assembler.withArguments {
        addAll '-target', 'i386-elf', '-gdwarf'
    }
    cCompiler.executable = 'clang'
    cCompiler.withArguments {
        addAll '-target', 'i386-elf', '-ffreestanding', '-gdwarf', '-nostdinc'
    }
    cppCompiler.executable = 'clang++'
    cppCompiler.withArguments {
        addAll '-target', 'i386-elf', '-ffreestanding', '-gdwarf', '-nostdinc'
    }
    linker.executable = 'clang'
    // #XXX: clang can't link target i386-elf with lld
    linker.withArguments {
        addAll '-target', 'i386-linux-elf', '-fuse-ld=lld', '-gdwarf', '-nostdlib', '-static',
            "-Wl,--script=${x86_32_elf_multiboot2_ld}"
    }
    staticLibArchiver.executable = 'llvm-ar'
}

final multiboot_x86_64 = {
    assembler.executable = 'clang'
    assembler.withArguments {
        addAll '-target', 'x86_64-elf', '-gdwarf'
    }
    cCompiler.executable = 'clang'
    cCompiler.withArguments {
        addAll '-target', 'x86_64-elf', '-ffreestanding', '-gdwarf', '-nostdinc'
    }
    cppCompiler.executable = 'clang++'
    cppCompiler.withArguments {
        addAll '-target', 'x86_64-elf', '-ffreestanding', '-gdwarf', '-nostdinc'
    }
    linker.executable = 'clang'
    // #XXX: clang can't link target x86_64-elf with lld
    linker.withArguments {
        addAll '-target', 'x86_64-linux-elf', '-fuse-ld=lld', '-gdwarf', '-nostdlib', '-static',
            "-Wl,--script=${x86_64_elf_multiboot2_ld}"
    }
    staticLibArchiver.executable = 'llvm-ar'
}

final uefi_x86_64 = {
    assembler.executable = 'clang'
    assembler.withArguments { addAll '-target', 'x86_64-unknown-windows' }
    cCompiler.executable = 'clang'
    cCompiler.withArguments {
        addAll '-target', 'x86_64-unknown-windows',
                '-ffreestanding', '-fshort-wchar', '-nostdinc'
    }
    cppCompiler.executable = 'clang++'
    cppCompiler.withArguments {
        addAll '-target', 'x86_64-unknown-windows',
            '-ffreestanding', '-fshort-wchar', '-nostdinc'
    }
    linker.executable = 'clang'
    // #XXX: clang can't link target i386-elf with lld
    linker.withArguments {
        addAll '-target', 'x86_64-unknown-windows', '-fuse-ld=lld',
            '-nostdlib', '-Wl,-entry:efi_main', '-Wl,-subsystem:efi_application'
    }
    staticLibArchiver.executable = 'llvm-ar'
}

project.model
{
    toolChains
    {
        llvm(Clang)
        {
            if (llvmPath.isPresent()) { it.path(llvmPath) }
            // host
            it.eachPlatform(host)
            // multiboot-x86_32
            it.target('linux-multiboot-x86_32', multiboot_x86_32)
            it.target('windows-multiboot-x86_32', multiboot_x86_32)
            // multiboot-x86_64
            it.target('linux-multiboot-x86_64', multiboot_x86_64)
            it.target('windows-multiboot-x86_64', multiboot_x86_64)
            // uefi-x86_64
            it.target('linux-uefi-x86_64', uefi_x86_64)
            it.target('windows-uefi-x86_64', uefi_x86_64)
        }
    }
}

plugins
{
    id 'cpp'
    id 'br.dev.pedrolamarao.gdb' apply false
}

subprojects
{
    // settings

    apply plugin: AsmLanguage
    apply plugin: 'br.dev.pedrolamarao.gdb'
    apply plugin: 'dev.nokee.cpp-application'

    // project

    application
    {
        // #XXX: Gradle can't cross compile to system "none"
        targetMachines = [ machines.linux.x86, machines.windows.x86 ]

        dependencies
        {
            implementation project(':multiboot2')
            implementation project(':pc')
            implementation project(':x86')
        }

        binaries.configureEach {

            compileTasks.configureEach {
                compilerArgs.addAll '-std=c++20', '-Werror', '-Wall', '-g'
            }

            final linker = it.linkTask.get()
            linker.linkerArgs.addAll '-std=c++20', '-Wl,-g', '-Wl,-Ttext=0x1000'

        }
    }

    final createMultibootRescue = tasks.register('createMultibootRescue', CreateMultibootRescue) {
        group = 'psys'
        inputFile = providers.provider {
            application.binaries
                .filter { it.isBuildable() }
                .map { it.get(0).linkTask.get().linkedFile.get() }
                .get()
        }
    }

    tasks.register('runMultibootRescueWindows', RunMultibootRescue) {
        group = 'psys'
        imageFile = createMultibootRescue.get().outputFile
    }

    def testRescue = tasks.register('testMultibootRescue', TestMultibootRescue) {
        group = 'psys'
        executableFile = providers.provider {
            application.binaries
                .filter { it.isBuildable() }
                .map { it.get(0).linkTask.get().linkedFile.get() }
                .get()
        }
        imageFile = createMultibootRescue.get().outputFile
    }

    qemuSystemTest.dependsOn testRescue
}
